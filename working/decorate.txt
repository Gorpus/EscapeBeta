//WEAPONS

actor Machete : Weapon replaces Chainsaw
{
  Weapon.SelectionOrder 3800
  Weapon.SlotNumber 1
  Weapon.Kickback 100
  Inventory.PickupMessage "Machete"
  +WEAPON.WIMPY_WEAPON
  +WEAPON.MELEEWEAPON
  +WEAPON.NOAUTOFIRE
  +WEAPON.NOAUTOAIM
  +NOEXTREMEDEATH
  +NOALERT
  Obituary "%o was cut up by %k's machete."
  States
  {
  Spawn:
    MHTE A -1
    loop
  Ready:
    MHTG G 1 A_WeaponReady
    loop
  Deselect:
    MHTG G 1 A_Lower
    loop
  Select: 
    MHTG G 1 A_Raise
    loop
  Fire:
    TNT1 A 0 A_Jump(128,"Fire4")
    TNT1 A 0 A_Jump(128,"Fire3")
    TNT1 A 0 A_Jump(128,"Fire2")
    MHTG A 2 A_PlayWeaponSound("MacheteSwing")
    MHTG B 2 
    MHTG C 4 A_CustomPunch (15,0,0,"MachetePuff")
    MHTG D 2
    MHTG E 2
    MHTG F 2
    MHTG G 2
    MHTG H 2 
    MHTG I 2 
    goto Ready
  Fire2:
    MHT2 A 2 A_PlayWeaponSound("MacheteSwing")
    MHT2 B 2 
    MHT2 C 4 A_CustomPunch (10,0,0,"MachetePuff")
    MHT2 D 2
    MHT2 E 2
    MHT2 F 2
    MHT2 G 2
    MHT2 H 2 
    goto Ready
  Fire3:
    MHT3 A 2 A_PlayWeaponSound("MacheteSwing")
    MHT3 B 2 
    MHT3 C 4 A_CustomPunch (5,0,0,"MachetePuff")
    MHT3 D 2
    MHT3 E 2
    MHT3 F 2
    MHT3 G 2
    TNT1 A 0 A_ReFire
    goto Ready
  Fire4:
    MHT4 A 2 A_PlayWeaponSound("MacheteSwing")
    MHT4 B 2 
    MHT4 C 4 A_CustomPunch (5,0,0,"MachetePuff")
    MHT4 D 2
    MHT4 E 2
    MHT4 F 2
    MHT4 G 2
    TNT1 A 0 A_ReFire
    goto Ready            
  }
}

ACTOR MachetePuff
{
  +NOBLOCKMAP
  +NOGRAVITY
  +NOEXTREMEDEATH
  +PUFFONACTORS
  RenderStyle Translucent
  Alpha 0.6
  SeeSound "MacheteHitThing"
  AttackSound "MacheteHitWall"
  ActiveSound "MacheteSwing"
  VSpeed 1
  States
  {
  Spawn:
    PUFF ABCD 4
    Stop
  }
}  

ACTOR PlasmaPistol : Weapon replaces PlasmaRifle
{
	Inventory.PickupMessage "Plasma Pistol"
	Weapon.AmmoType1 "PPClip"
	Weapon.AmmoUse1 1
	weapon.AmmoGive1 20
	Weapon.AmmoType2 "Battery"
	Weapon.AmmoUse2 1
	Weapon.SlotNumber 2
	+WEAPON.NOAUTOAIM
	+NOEXTREMEDEATH
	+INVENTORY.NOSCREENFLASH
	States
	{
	Spawn:
	  PLPP A -1
	  Stop
	Ready:
	  PLPI A 1 A_WeaponReady
	  Loop
	Select:
	  PLPI A 1 A_Raise
	  Loop
	Deselect:
	  PLPI A 1 A_Lower
	  Loop
	Fire:
	  PLPI A 0 A_PlayWeaponSound("PlasmaFire")
	  PLPI A 1 A_FireCustomMissile ("PPShot",0,1,0,0,0)
	  PLPI B 4
	  PLPI C 4
	  PLPI B 2
	  PLPI A 6
	  Goto Ready
	AltFire:
	  PLPI A 2
	  PLPR ABC 4
	  PLPR D 0 A_TakeInventory ("Battery", 1)
	  PLPR D 6 A_GiveInventory ("PPClip", 20)
	  PLPR CBA 2
	  Goto Ready
	}
}

ACTOR PPShot : PlasmaBall
{
	Radius 7
	Speed 40
	Damage 3
	SeeSound "PlasmaFire"
	Obituary "%o was melted by %k's plasma pistol."
	States
	{
	Spawn:
	  BOLT AB 6 Bright
	  Loop
	Death:
	  BOLT ABCDEF 3 Bright
	  Stop
	}
}

ACTOR Battery : Ammo replaces Cell
{
	Inventory.PickupMessage "Battery"
	Inventory.Amount 1
	Inventory.MaxAmount 2
	Inventory.Icon "CELLA0"
	Ammo.BackpackAmount 3
	Ammo.BackpackMaxAmount 8
	States
	{
	Spawn:
	  CELL A -1
	  Stop
	}
}

ACTOR PPClip : Ammo
{
	Inventory.MaxAmount 20
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 20
}

ACTOR Machinegun : Weapon replaces Chaingun
{
  Obituary "%o was gunned down by %k's machinegun."
  Inventory.PickupMessage "Machinegun"
  Weapon.AmmoType1 "MGClip"
  Weapon.AmmoUse1 1
  Weapon.AmmoGive1 35
  Weapon.AmmoType2 "Magazine"
  Weapon.AmmoUse2 1
  Weapon.SlotNumber 3
  +WEAPON.NOAUTOAIM
  +NOEXTREMEDEATH
  +INVENTORY.NOSCREENFLASH
  states
  {
  Ready:
    MGUN A 1 A_WeaponReady
    loop
  Deselect: 
    MGUN A 1 A_Lower
    loop
  Select:
    MGUN A 1 A_Raise
    loop
  Fire:
	MGUN A 0 A_PlayWeaponSound("MGun/Fire")
	MGUN A 1 A_FireBullets(4,3,1,6,"BulletPuff")
	MGUN BC 2 Bright
	MGUN C 0 A_Refire
	Goto Ready
  AltFire:
    MGUN A 2
	MGNR AB 6
	MGNR C 0 A_TakeInventory ("Magazine", 1)
	MGNR C 9 A_GiveInventory ("MGClip", 35)
	MGNR BA 3
	Goto Ready
  Spawn:
    MGUP A -1
    stop
  }
}

ACTOR Magazine : Ammo replaces Clip
{
	Inventory.PickupMessage "Magazines"
	Inventory.Amount 2
	Inventory.MaxAmount 2
	Inventory.Icon "AMOKA0"
	Ammo.BackpackAmount 1
	Ammo.BackpackMaxAmount 7
	States
	{
	Spawn:
	  AMOK A -1
	  Stop
	}
}

ACTOR MGClip : Ammo
{
	Inventory.MaxAmount 35
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 35
}

ACTOR PulseRifle : Weapon replaces Shotgun
{
  Inventory.PickupMessage "Pulse Rifle"
  Weapon.AmmoType1 "PRClip"
  Weapon.AmmoUse1 1
  Weapon.AmmoGive1 2
  Weapon.AmmoType2 "Battery"
  Weapon.AmmoUse2 1
  Weapon.SlotNumber 4
  +WEAPON.NOAUTOAIM
  +NOEXTREMEDEATH
  +INVENTORY.NOSCREENFLASH
  states
  {
  Ready:
    PULS A 1 A_WeaponReady
    loop
  Deselect: 
    PULS A 1 A_Lower
    loop
  Select:
    PULS A 1 A_Raise
    loop
  Fire:
	PULS A 0 A_PlayWeaponSound("PlasmaFire")
	PULS A 1 A_FireCustomMissile("PRShot",-10,1,0,1,0)
	PULS A 0 A_FireCustomMissile("PRShot",10,0,0,1,0)
	PULS A 0 A_FireCustomMissile("PRShot",5,0,0,1,5)
	PULS A 0 A_FireCustomMissile("PRShot",-5,0,0,1,-5)
	PULS A 0 A_FireCustomMissile("PRShot",5,0,0,1,-5)
	PULS A 0 A_FireCustomMissile("PRShot",-5,0,0,1,5)
	PULS A 0 A_FireCustomMissile("PRShot",0,0,0,1,0)
	PULS BC 4 Bright
	PULS A 15
	Goto Ready
  AltFire:
    PLSR A 2
	PLSR AB 8
	PLSR C 0 A_TakeInventory ("Battery", 1)
	PLSR C 12 A_GiveInventory ("PRClip", 2)
	PLSR BA 4
	Goto Ready
  Spawn:
    PLSP A -1
    stop
  }
}
ACTOR PRShot : PPShot
{
  Speed 30
  Damage 6
  Obituary "%o was blasted by %k's pulse rifle."
  +NOEXTREMEDEATH
  States
  {
	Spawn:
	  PRST AB 6 Bright
	  Loop
	Death:
	  PRST ABCDEF 3 Bright
	  Stop
	}
}

ACTOR PRClip : Ammo
{
	Inventory.MaxAmount 2
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 2
}

ACTOR Rifle : Weapon replaces SuperShotgun
{
  Obituary "%o was shot down by %k's machinegun."
  Inventory.PickupMessage "Rifle"
  Weapon.AmmoType1 "RFClip"
  Weapon.AmmoUse1 1
  Weapon.AmmoGive1 7
  Weapon.AmmoType2 "RifleClip"
  Weapon.AmmoUse2 1
  Weapon.SlotNumber 5
  +WEAPON.NOAUTOAIM
  +NOEXTREMEDEATH
  +INVENTORY.NOSCREENFLASH
  +WEAPON.NOAUTOFIRE
  states
  {
  Ready:
    RIFG A 1 A_WeaponReady
    loop
  Deselect: 
    RIFG A 1 A_Lower
    loop
  Select:
    RIFG A 1 A_Raise
    loop
  Fire:
    RIFG A 0 A_PlayWeaponSound("RifleShot")
	RIFG A 0 A_GunFlash
	RIFG A 4 A_FireBullets(1,1,1,45,"BulletPuff")
	RIFG BC 6
	RIFG D 10
	RIFG CB 4
	RIFG A 2
	Goto Ready
  Flash:
    RIFF AB 2 Bright
	stop
  AltFire:
	RIFG A 2
	RIFR ABC 10
	RIFR D 0 A_TakeInventory("RifleClip",1)
	RIFR D 15 A_GiveInventory("RFClip",7)
	RIFR CBA 5
	Goto Ready
  Spawn:
    RIFL A -1
    stop
  }
}

ACTOR RifleClip : Ammo replaces Shell
{
	Inventory.PickupMessage "Rifle Clip"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Inventory.Icon "RIFPA0"
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 3
	States
	{
	Spawn:
	  RIFP A -1
	  Stop
	}
}

ACTOR RFClip : Ammo
{
	Inventory.MaxAmount 7
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 7
}


//ITEMS

ACTOR Backpack2 : Backpack replaces ClipBox
{
	States
	{
		Spawn:
			BPK2 A -1
			stop
	}
}

